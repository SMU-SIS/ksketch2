<?xml version="1.0" encoding="utf-8"?>
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009"
		xmlns:view="sg.edu.smu.ksketch2.canvas.components.view.*"
		xmlns:buttons="sg.edu.smu.ksketch2.canvas.components.buttons.*" 
		xmlns:s="library://ns.adobe.com/flex/spark" actionBarVisible="false" 
		xmlns:popup="sg.edu.smu.ksketch2.canvas.components.popup.*"
		viewActivate="viewActivateHandler(event)"
		>
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
		<s:HTTPService id="GetUser"               
					   resultFormat="text" 
					   result="ParseJSONObject(event)" 
					   showBusyCursor="false">                        
		</s:HTTPService>
	</fx:Declarations>
	<fx:Script>
        
		<![CDATA[
			import com.adobe.serialization.json.JSON;
			
			import flash.events.StatusEvent;
			import flash.media.StageWebView;
			import flash.net.URLRequest;
			
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			
			import spark.events.ViewNavigatorEvent;
			
			import air.net.URLMonitor;
			
			import sg.edu.smu.ksketch2.KSketchWebLinks;
			import sg.edu.smu.ksketch2.KSketch_User;
			import sg.edu.smu.ksketch2.canvas.components.popup.KSketch_DataProgressBar;
			import sg.edu.smu.ksketch2.canvas.components.view.KSketch_HomeView;
			
			private var progressTimer:Timer;
			private var _stageWidth:Number;
			private var _stageHeight:Number;
			private var monitor:URLMonitor;
			private var monitorTimer:Timer;
			
			public static var userData:Object = new Object();
			private var webView:StageWebView = new StageWebView();
			
			public static var _isLoggedIn:Boolean = false;
			public static var _isLoggedOut:Boolean = false;
			
			
			protected function viewActivateHandler(event:ViewNavigatorEvent):void
			{
				// TODO Auto-generated method stub
				
				data = null;
				webView.stage = stage;
				_stageWidth = stage.stageWidth;
				_stageHeight = stage.stageHeight;
				
				//check for network connection
				monitorTimer = new Timer(3000);
				monitor = new URLMonitor(new URLRequest(KSketchWebLinks.url));
				
				monitorTimer.addEventListener(TimerEvent.TIMER, endMonitor);
				monitor.addEventListener(StatusEvent.STATUS, prepareView);
				
				monitorTimer.start();
				monitor.start();
			}
			
			private function endMonitor(event:TimerEvent):void
			{
				monitorTimer.stop();
				monitor.stop();
				
				monitorTimer.removeEventListener(TimerEvent.TIMER, endMonitor);
				monitor.removeEventListener(StatusEvent.STATUS, prepareView);
				
				skipConnection("Lost Connection");
			}
			
			private function prepareView(event:StatusEvent):void
			{
				//only show Webview (which is Janrain login page) when there is a connection
				if(monitor.available)
				{
					if(_stageWidth > _stageHeight)
						webView.viewPort = new Rectangle(0, 0, _stageWidth, _stageHeight);
					else
						webView.viewPort = new Rectangle(0, 0, _stageHeight, _stageWidth);
					
					webView.loadURL(KSketchWebLinks.url);
					webView.addEventListener(flash.events.LocationChangeEvent.LOCATION_CHANGE, locationChangedHandler);				//for IOS
					webView.addEventListener(flash.events.LocationChangeEvent.LOCATION_CHANGING, locationChangingHandler);			//for ANDROID
					webView.addEventListener(ErrorEvent.ERROR, errorEventHandler);
					
				}
				else
				{
					if(_isLoggedOut)
						skipConnection("Skip Login");
					else
						skipConnection("Lost Connection");
				}
				
				monitorTimer.stop();
				monitor.stop();
				
				monitor.removeEventListener(StatusEvent.STATUS, prepareView);
				monitorTimer.removeEventListener(TimerEvent.TIMER, endMonitor);
			}
			
			public function skipConnection(errorHandled:String):void
			{	
				if(GetUser.hasEventListener(FaultEvent.FAULT))
					GetUser.removeEventListener(FaultEvent.FAULT, faultHandler);
					
				if(webView)
				{
					//remove error event listeners
					if(webView.hasEventListener(ErrorEvent.ERROR))
						webView.removeEventListener(ErrorEvent.ERROR, errorEventHandler);
					
					webView.stop();
					webView.viewPort = null;
					webView.dispose();
					webView = null;
				}
				
				_isLoggedOut = false;
				navigator.pushView(KSketch_HomeView, errorHandled);
			}
			
			//FOR Android
			private function locationChangingHandler(event:LocationChangeEvent):void
			{
				var currentURL:String = event.location.toString();
				
				//if successful login
				if(currentURL.indexOf(KSketchWebLinks.redirecturl_login) >= 0) 
				{
					//retrieve id from HTML page
					webView.loadURL(KSketchWebLinks.urlUser);
				}
				else if((currentURL.indexOf(KSketchWebLinks.redirecturl_skip) >= 0) || (currentURL.indexOf(KSketchWebLinks.redirecturl_index) >= 0))
				{
					skipConnection("Skip Login");
				}
			}
			
			//FOR IOS
			private function locationChangedHandler(event:LocationChangeEvent):void
			{
				var currentURL:String = event.location.toString();
				
				if(currentURL.indexOf(KSketchWebLinks.redirecturl_login) >= 0) 
				{
					//retrieve id from HTML page
					webView.loadURL(KSketchWebLinks.urlUser);
				}
				else if ((currentURL.indexOf(KSketchWebLinks.login_success) >= 0))
				{
					webView.stop();
					webView.viewPort = null;
					webView.dispose();
					webView = null;
					
					var arr:Array = currentURL.split(".html?");
					var vars:URLVariables = new URLVariables(arr[1]);
					
					//get json url
					GetUser.url = KSketchWebLinks.jsonurlUserMobile + "?id=" + vars.id;
					GetUser.send();
					GetUser.addEventListener(FaultEvent.FAULT, faultHandler);
					startProgressBar();
				}
				else if((currentURL.indexOf(KSketchWebLinks.redirecturl_skip) >= 0) || (currentURL.indexOf(KSketchWebLinks.redirecturl_index) >= 0))
				{
					skipConnection("Skip Login");
				}
			}
			
			public function ParseJSONObject(event:ResultEvent):void
			{
				var rawData:String = String(event.result);
				trace("Retrieved data: " + rawData);
				var obj:Object = com.adobe.serialization.json.JSON.decode(rawData,true);
				
				//check if user data does not exist
				if(obj != null)
				{
					// create a new User instance and
					// save it to data property
					var kUser:KSketch_User = new KSketch_User(obj);
					userData.kUser = kUser;
							
					_isLoggedIn = true;
					
					navigator.pushView(KSketch_HomeView, userData);
				}
					
				progressTimer.stop();
				progressTimer.removeEventListener(TimerEvent.TIMER, timer_tickHandler);
				progressBar.visible = false;
			}
			
			protected function startProgressBar():void
			{
				progressBar.visible = true;
				
				progressTimer = new Timer(1);
				progressTimer.addEventListener(TimerEvent.TIMER, timer_tickHandler);
				progressTimer.start();
			}
			
			protected function timer_tickHandler(event:TimerEvent):void
			{
				progressBar.value += .01;
				
				if (progressBar.value >= 1)
					progressBar.value = 0;
			}
			
			private function faultHandler(event:FaultEvent):void
			{
				skipConnection("Janrain Failed");
			}
			
			private function errorEventHandler(event:ErrorEvent):void
			{
				skipConnection("Lost Connection");
			}
			
		]]>
	</fx:Script>
	<popup:DataProgressBar id="progressBar" skinClass="sg.edu.smu.ksketch2.canvas.components.popup.KSketch_DataProgressBar"
						   horizontalCenter="0" verticalCenter="0" visible="false"/>
	<!--
	<s:Group width="100%" height="100%">
		<s:Rect width="100%" height="100%">
			<s:fill> <s:SolidColor color="0xE7E7E7"/> </s:fill>
		</s:Rect>
		
		<s:Group width="100%" height="100%">
			<s:layout> <s:VerticalLayout gap="2"/> </s:layout>
			
			<s:Group id="disconnectedContent" width="100%" height="100%" visible="false">
				<s:layout> <s:HorizontalLayout gap="5" paddingLeft="50" paddingRight="50" paddingTop="50"/> </s:layout>
				
				<s:Group width="40%" height="40%">
					<s:layout> <s:HorizontalLayout paddingLeft="68" paddingTop="10"/> </s:layout>
					<buttons:KSketch_Button id="logoHolder"
											width="{KSketchStyles.LOGO_BUTTON_WIDTH}" 
											height="{KSketchStyles.LOGO_BUTTON_HEIGHT}"/>
				</s:Group>
				
				
				<s:Group width="60%">
					<s:layout> <s:VerticalLayout paddingLeft="10" paddingTop="50"/> </s:layout>
					<s:Label width="80%" fontFamily="{KSketchStyles.APP_FONT_FAMILY}" 
							 fontSize="{KSketchStyles.LOGIN_CONNECTION_FONT_SIZE}"
							 text="Whoops! No network connection is detected, though it would be great to have you logged in. What would you like to do?" />
					
					<s:Group width="100%">
						<s:layout><s:HorizontalLayout gap="50" paddingTop="30"/></s:layout>
						
						<s:Button width="25%" label="Try Again" click="checkNetworkConnection()"/>
						<s:Button width="25%" label="Skip" click="skipConnection()"/>
					</s:Group>
				</s:Group>
			</s:Group>
		</s:Group>
	</s:Group>
	-->
</s:View>