<?xml version="1.0" encoding="utf-8"?>
<!--
*Copyright 2010-2012 Singapore Management University
*Developed under a grant from the Singapore-MIT GAMBIT Game Lab

*This Source Code Form is subject to the terms of the
*Mozilla Public License, v. 2.0. If a copy of the MPL was
*not distributed with this file, You can obtain one at
*http://mozilla.org/MPL/2.0/.
-->
<s:Button xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark"
		 skinClass="sg.edu.smu.ksketch2.canvas.components.buttons.KSketch_Canvas_Button_Skin"
		 initialize="{buttonState = SHOW_PLAY}">
	<fx:Script>
		<![CDATA[
			import mx.core.FlexGlobals;
			import mx.core.UIComponent;
			
			import spark.components.Application;
			import spark.components.Image;
			import spark.components.SkinnablePopUpContainer;
			
			import sg.edu.smu.ksketch2.KSketch2;
			import sg.edu.smu.ksketch2.KSketchAssets;
			import sg.edu.smu.ksketch2.KSketchStyles;
			import sg.edu.smu.ksketch2.canvas.components.timebar.KSketch_TimeControl;
			import sg.edu.smu.ksketch2.canvas.controls.KInteractionControl;
			import sg.edu.smu.ksketch2.utils.KSelection;
			
			private var _KSketch:KSketch2;
			private var _interceptor:UIComponent;
			private var _interactionControl:KInteractionControl;
			private var _timeControl:KSketch_TimeControl;
			private var _buttonState:int;
			private var _oldSelection:KSelection;
			private var _menu:SkinnablePopUpContainer;
			private var _wasMenuOpen:Boolean = false;
			
			private const SHOW_PLAY:int = 0;
			private const SHOW_PAUSE:int = 1;
			
			
			public function init(timeControl:KSketch_TimeControl, interactionControl:KInteractionControl,
								KSketchInstance:KSketch2, menu:SkinnablePopUpContainer):void
			{
				_KSketch = KSketchInstance;
				_interactionControl = interactionControl;
				_timeControl = timeControl;
				_menu = menu;
				
				timeControl.addEventListener(KSketch_TimeControl.PLAY_START, _handlePlayPause);
				timeControl.addEventListener(KSketch_TimeControl.PLAY_STOP, _handlePlayPause);
			}
			
			public function playToggle():void
			{

				if(_buttonState == SHOW_PLAY)
					_timeControl.play();
				else
					_timeControl.stop();
			}
			
			private function _handlePlayPause(event:Event):void
			{
				var app:Application = FlexGlobals.topLevelApplication as Application;
				//_interceptor.graphics.clear();
				
				if(event.type == KSketch_TimeControl.PLAY_START)
				{
					buttonState = SHOW_PAUSE;
					_oldSelection =_interactionControl.selection;
					_interactionControl.selection = null; 
					
					if(_menu.isOpen)
					{
						_menu.close();
						_wasMenuOpen = true;
					}
					else
						_wasMenuOpen = false;		
				}
				else
				{
					buttonState = SHOW_PLAY;	// IF IT IS PAUSING,THEN SHOW PLAY!
					
					if(_wasMenuOpen)
						_menu.open(app);
				}
			}
			
			private function set buttonState(value:int):void
			{
				var textureControl:Image = (skin as KSketch_Canvas_Button_Skin).texture;

				_buttonState = value;

				if(_buttonState == SHOW_PLAY)
					textureControl.source = KSketchAssets.texture_play;
				else
					textureControl.source = KSketchAssets.texture_pause;
				
				(skin as KSketch_Canvas_Button_Skin).upColor = KSketchStyles.CANVAS_BUTTON_ACTIVATED;
			}
		]]>
	</fx:Script>
</s:Button>

