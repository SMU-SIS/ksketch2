<?xml version="1.0" encoding="utf-8"?>
<!--
*Copyright 2010-2012 Singapore Management University
*Developed under a grant from the Singapore-MIT GAMBIT Game Lab

*This Source Code Form is subject to the terms of the
*Mozilla Public License, v. 2.0. If a copy of the MPL was
*not distributed with this file, You can obtain one at
*http://mozilla.org/MPL/2.0/.
-->
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009" 
		xmlns:s="library://ns.adobe.com/flex/spark" actionBarVisible="false" 
		xmlns:view="sg.edu.smu.ksketch2.canvas.components.view.*"
		xmlns:controls="sg.edu.smu.ksketch2.canvas.controls.*"
		xmlns:buttons="sg.edu.smu.ksketch2.canvas.components.buttons.*" 
		xmlns:transformWidget2="sg.edu.smu.ksketch2.canvas.components.transformWidget.*"
		xmlns:timebar="sg.edu.smu.ksketch2.canvas.components.timebar.*"
		addedToStage="initializeTrackerForWeb()"
		initialize="_initCanvas(false)"
		add="_checkForImageData()"
		creationPolicy="all"
		destructionPolicy="auto" 
		viewActivate="_enterView_ProcessData()" xmlns:ns="http://www.google.com/analytics/2009"
		>
	<fx:Script>
		<![CDATA[
			import com.google.analytics.AnalyticsTracker;
			import com.google.analytics.GATracker;
			
			import mx.collections.ArrayCollection;
			import mx.collections.Sort;
			import mx.collections.SortField;
			import mx.events.FlexEvent;
			import mx.graphics.codec.JPEGEncoder;
			import mx.utils.Base64Encoder;
			
			import spark.events.ViewNavigatorEvent;
			import spark.managers.PersistenceManager;
			
			import sg.edu.smu.ksketch2.KSketch2;
			import sg.edu.smu.ksketch2.KSketchStyles;
			import sg.edu.smu.ksketch2.canvas.components.popup.KSketch_Alert;
			import sg.edu.smu.ksketch2.canvas.components.popup.KSketch_Feedback_Message;
			import sg.edu.smu.ksketch2.canvas.components.popup.KSketch_InstructionsBox;
			import sg.edu.smu.ksketch2.canvas.components.popup.KSketch_Options;
			import sg.edu.smu.ksketch2.canvas.components.popup.KSketch_OverwriteSaveOptions;
			import sg.edu.smu.ksketch2.canvas.components.popup.KSketch_PopUp_Menu;
			import sg.edu.smu.ksketch2.canvas.components.popup.KSketch_SaveOptions;
			import sg.edu.smu.ksketch2.canvas.components.popup.KSketch_Timebar_Context_Double;
			import sg.edu.smu.ksketch2.canvas.components.popup.KSketch_Timebar_Context_Single;
			import sg.edu.smu.ksketch2.canvas.components.popup.KSketch_Timebar_Magnifier;
			import sg.edu.smu.ksketch2.canvas.components.timebar.KSketch_TickMark_Control;
			import sg.edu.smu.ksketch2.canvas.components.timebar.KSketch_TimeSkip_Button;
			import sg.edu.smu.ksketch2.canvas.components.transformWidget.KSketch_Widget_Component;
			import sg.edu.smu.ksketch2.canvas.controls.KActivityControl;
			import sg.edu.smu.ksketch2.canvas.controls.KInteractionControl;
			import sg.edu.smu.ksketch2.canvas.controls.KMobileControl;
			import sg.edu.smu.ksketch2.canvas.controls.interactors.KCanvasInteractorManager;
			import sg.edu.smu.ksketch2.canvas.controls.interactors.widgetstates.KWidgetInteractorManager;
			import sg.edu.smu.ksketch2.events.KSketchEvent;
			import sg.edu.smu.ksketch2.model.data_structures.KModelObjectList;
			import sg.edu.smu.ksketch2.model.objects.KGroup;
			import sg.edu.smu.ksketch2.model.objects.KInstructions;
			import sg.edu.smu.ksketch2.model.objects.KObject;
			import sg.edu.smu.ksketch2.operators.operations.KCompositeOperation;
			import sg.edu.smu.ksketch2.utils.KIOEvent;
			import sg.edu.smu.ksketch2.utils.KSketchDocument;
			
			public static const PLUG_IN:String = "PlugIn";
			
			[Bindable]
			public static var isPlayer:Boolean = false;
			
			[Bindable]
			public static var isMobile:Boolean = false;
			
			[Bindable]
			public static var isWeb:Boolean = true;
			
			[Bindable]
			public static var isWebViewer:Boolean = false;
			
			public static var createNewSketch:Boolean = false;
			
			public static var tracker:AnalyticsTracker = null;
			
			private var userData:Object = new Object();
			private var _imageData:BitmapData;
			private var _needToProcessData:Boolean;
			private var _groupObjectsList:ArrayCollection;
			public var _currentDocument:KSketchDocument;
			public var enableLog:Boolean = false;
			
			private var _KSketch:KSketch2;
			public var mobileControl:KMobileControl;
			public var _interactionControl:KInteractionControl;
			public var _isNewSketch:Boolean = true;
			
			//Pop Ups
			private var _feedbackPopUp:KSketch_Feedback_Message;
			private var _menuPopUp:KSketch_PopUp_Menu;
			private var _options:KSketch_Options;
			private var _instructionsBox:KSketch_InstructionsBox;
			private var _kAlertBox:KSketch_Alert;
			private var _saveOptions:KSketch_SaveOptions;
			private var _overwriteSaveOptions:KSketch_OverwriteSaveOptions;
			private var _magnifier:KSketch_Timebar_Magnifier;
			private var _contextDouble:KSketch_Timebar_Context_Double;
			private var _contextSingle:KSketch_Timebar_Context_Single;
			private var _transitionHelper:KWidgetInteractorManager;
			private var _selectionHelper:KCanvasInteractorManager;
			private var _kInstructions:KInstructions;
			
			//KSKETCH-SYNPHNE
			private var _activityControl:KActivityControl;
			
			protected var persistanceManager:PersistenceManager = new PersistenceManager();
			
			//Starting up is hopefully done in the following stages
			//Should not depend on the events beingfired in the correct sequence all the time
			//addedToStage: _initCanvas (if needed)
			//add: _checkForImageData() //This can be done during addedToStage too
			//viewActivated: _viewActivateHandler - loads data/ insert images 
			//				A lot of times, this happens first, so we go back to addedToStage
			private function _saveHandler(event:KIOEvent):void
			{
				if(event.saveData)
				{
					if(ExternalInterface.available)
						ExternalInterface.call("saveKSketchFile", event.saveData.toXMLString());
				}
			}
			
			private function _loadHandler(event:KIOEvent):void
			{
				if(ExternalInterface.available)
					ExternalInterface.call("initLoad");
			}
			
			public function initializeTrackerForWeb():void
			{
				if(isWeb && !tracker)
					tracker = new GATracker(this, "UA-47832938-1", "AS3", false);
			}
			
			//Starts up the canvas in the following order
			public function _initCanvas(isView:Boolean):void
			{	
				setRegionVisibility(false);
				
				this.removeEventListener(FlexEvent.INITIALIZE, _initCanvas);
				
				userData = data;
				
				isWebViewer = isView;
				
				if(!tracker && !isWeb)
					tracker = new GATracker(this, "UA-47832938-1", "AS3", true);
				
				//We shouldn't make a new ksketch if one already exists
				if(_KSketch)
					return;
				
				//Determine platform type, mobile / not mobile
				//If os is a mac or windows, it is desktop.
				if ((Capabilities.os.toLowerCase().indexOf("mac") == -1) &&
					(Capabilities.os.toLowerCase().indexOf("windows") == -1))
					isMobile = true;
				else
					isMobile = false;
				
				isWeb = (Capabilities.playerType == PLUG_IN);
				if(isWeb)
				{
					Security.allowDomain("*");
					
					this.addEventListener(KIOEvent.EVENT_SAVE, _saveHandler);
					this.addEventListener(KIOEvent.EVENT_LOAD, _loadHandler);
					
					if(ExternalInterface.available)
					{
						ExternalInterface.addCallback("save", saveTrigger);
						ExternalInterface.addCallback("load", loadTrigger);
					}
					else
						throw new Error("Load call back not added");
				}
				
				if(!isWeb)
					mobileControl = userData.mobileControl;
				
				//Actual setting up starts here
				_KSketch = new KSketch2();
				_interactionControl = new KInteractionControl(_KSketch, timeControl);			
				//Initiate pop ups
				_feedbackPopUp = new KSketch_Feedback_Message();
				_feedbackPopUp.init(this);
				_options = new KSketch_Options();
				_instructionsBox = new KSketch_InstructionsBox();
				_contextDouble = new KSketch_Timebar_Context_Double();
				_contextSingle = new KSketch_Timebar_Context_Single();
				_saveOptions = new KSketch_SaveOptions();
				_overwriteSaveOptions = new KSketch_OverwriteSaveOptions();
				_menuPopUp = new KSketch_PopUp_Menu();
				_magnifier = new KSketch_Timebar_Magnifier();
				_magnifier.init(canvas_component, timeControl);
				_options.init(this, _menuPopUp);
				
				//Initiate the controls
				_activityControl = new KActivityControl(_instructionsBox, this, _KSketch, _interactionControl);
				
				if(!isPlayer)
				{
					_transitionHelper = new KWidgetInteractorManager(_KSketch, _interactionControl, widget, modelDisplay);
					_selectionHelper = new KCanvasInteractorManager(_KSketch, _interactionControl, _activityControl, this, modelDisplay, motionDisplay, _feedbackPopUp);
				}
				
				
				if(!isWeb)
				{
					_saveOptions.init(this, _menuPopUp, _overwriteSaveOptions);
					_overwriteSaveOptions.init(this, _saveOptions);
					_kAlertBox = new KSketch_Alert();
					_kAlertBox.createDeferredContent();
					_menuPopUp.init(canvas_component, this, _options, _instructionsBox, _saveOptions, _KSketch, _interactionControl, _selectionHelper, _feedbackPopUp, _kAlertBox);
					_saveOptions.createDeferredContent();
				}
				
				_options.createDeferredContent();
				_instructionsBox.createDeferredContent();
				_menuPopUp.createDeferredContent();
				
				if(isWebViewer)
					_menuPopUp.alpha = 0;
				
				var timeTickControl:KSketch_TickMark_Control = new KSketch_TickMark_Control(_KSketch, timeControl,_interactionControl);
				timeControl.init(_KSketch, timeTickControl, _transitionHelper, _magnifier, _contextDouble, _contextSingle);
				
				leftTimeJumper.init(_KSketch, timeControl, this, KSketch_TimeSkip_Button.LEFT);
				rightTimeJumper.init(_KSketch, timeControl, this, KSketch_TimeSkip_Button.RIGHT);
				
				_contextDouble.init(_KSketch, _interactionControl, timeTickControl, timeControl);
				_contextDouble.createDeferredContent();
				
				_contextSingle.init(_KSketch, _interactionControl, timeControl);
				_contextSingle.createDeferredContent();
				
				modelDisplay.init(_KSketch, _interactionControl, true);
				motionDisplay.init(_KSketch, _interactionControl);
				
				//This is the layer that blocks interaction whenever the app is playing
				playButton.init(timeControl, _interactionControl, this, _KSketch, _menuPopUp);
				
				_interactionControl.reset();
				
				_options.applyPreferences();
				_magnifier.open(this);
				
				//Fail safe measure
				if(_needToProcessData)
					_enterView_ProcessData();
			}
			
			private function _checkForImageData():void
			{
				if(navigator.poppedViewReturnedObject)
					_imageData = navigator.poppedViewReturnedObject.object as BitmapData;
			}
			
			//The view activate handler should only be used solely for loading/generating ksketch objects
			//Interface initiation should be done when the view has been added to stage
			//This should be the last process of canvas start up.
			private function _enterView_ProcessData():void
			{	
				if(!tracker && !isWeb)
					tracker = new GATracker(this, "UA-47832938-1", "AS3", false);
				
				_KSketch.time = 0;
				_KSketch.beginSession();
				
				//Fail safe measure, we cannot depend on start up events firing properly
				//Cancel activate handler if the events screwed up their order
				if(!_KSketch)
				{
					_needToProcessData = true;
					return;
				}
				else
					_needToProcessData = false;
				
				if(_imageData)
				{
					//Add image here
					if(_imageData)
					{
						var op:KCompositeOperation = new KCompositeOperation();
						_KSketch.object_Add_Image(_imageData, _KSketch.time, op);
						_interactionControl.addToUndoStack(op);
					}
					_imageData = null;
				}
				else
				{
					if(userData != null)
					{
						if(userData.kSketchDocument && userData.kSketchDocument is KSketchDocument)
						{
							_isNewSketch = false;
							_currentDocument = userData.kSketchDocument as KSketchDocument;
							
							if(_currentDocument.xml.scene.children().length() > 0)
								_KSketch.generateSceneFromXML(new XML(_currentDocument.xml.scene));
							
							_activityControl.isNewSketch = false;
							if(_currentDocument.xml.activities != undefined) 
							{
								_kInstructions = new KInstructions(new XML(_currentDocument.xml.activities));
								_instructionsBox.init(this, _menuPopUp, _kInstructions, _kAlertBox, _activityControl);
							}
						}	
						else
						{
							_activityControl.isNewSketch = true;
							_menuPopUp.initInstructions();
						}
					}
					
					data = null;
				}
				
				_groupObjectsList = new ArrayCollection();
				_getGroupObjectsOnLoad(_KSketch.root.children);
				
				if(_groupObjectsList.length > 0)
				{
					//sort array by smallest id to largest
					var dataSortField:SortField = new SortField();
					dataSortField.name = "id";
					var numericDataSort:Sort = new Sort();
					numericDataSort.fields = [dataSortField];
					_groupObjectsList.sort = numericDataSort;
					_groupObjectsList.refresh();
					
					for(var x:int=0; x<_groupObjectsList.length; x++)
						modelDisplay.view_addObject(_groupObjectsList.getItemAt(x) as KObject);
					
					// broadcast that the model has been updated 
					_KSketch.dispatchEvent(new KSketchEvent(KSketchEvent.EVENT_MODEL_UPDATED, _KSketch.root));
				}
				
				_groupObjectsList.removeAll();
				
				_options.applyPreferences();
				_magnifier.open(this);
			}
			
			private function _getGroupObjectsOnLoad(list:KModelObjectList):void
			{
				if(list.length() > 0)
				{
					for(var i:int=0; i<list.length(); i++)
					{
						if(list.getObjectAt(i) is KGroup)
						{
							for(var x:int=0; x<list.length(); x++)
							{
								if(list.getObjectAt(x) is KGroup)
									_getGroupObjectsOnLoad((list.getObjectAt(x) as KGroup).children);
							}
							_groupObjectsList.addItem(list.getObjectAt(i));
						}
					}
				}
			}
			
			public function saveSketch(confirmSave:Boolean):Object
			{
				userData.resolveConflict = 1;
				
				//if save is confirmed, set all interaction selection to null
				//to generate an "unselected" thumbnail
				if(confirmSave)
				{
					if(_interactionControl.selection)
						_interactionControl.selection = null;
				}
				
				//get existing KSketchDocument object
				var kSketchDocumentObj:KSketchDocument = (createReturnObject() as KSketchDocument); 
				
				if(kSketchDocumentObj != null)
				{
					userData.name = kSketchDocumentObj.name.toString();
					userData.lowerFileName = kSketchDocumentObj.name.toString().toLowerCase();
					
					var fileData:String = kSketchDocumentObj.xml.toXMLString();
					
					userData.kSketchDocument = kSketchDocumentObj;
					
					var thumbnailLength:int = userData.kSketchDocument.xml.child("thumbnail").length();
					
					if(thumbnailLength > 0)
					{
						var recentThumbnail:int = thumbnailLength - 1;
						userData.thumbnailData = userData.kSketchDocument.xml.child("thumbnail")[recentThumbnail].@data.toString();
					}else
					{
						userData.thumbnailData = userData.kSketchDocument.xml.child("thumbnail").@data.toString();
					}
					
					return userData;
				}
				
				return null;
			}
			
			public function exit():void
			{
				viewDeactivated();
				_saveOptions.canvasDirty = false;
				KSketch_SaveOptions.saveCounter = 0;
				
				KSketch_HomeView.canvasTimerDisabled = true;
				if(KSketch_HomeView.hasBeenNotified)
					KSketch_HomeView.hasBeenNotified = true;
				
				navigator.pushView(KSketch_HomeView, userData);
				
				System.pauseForGCIfCollectionImminent();
			}
			
			public function viewDeactivated():void
			{
				//comment out these two lines to get toolbar stay when it is reactivated.
				_options.close();
				_instructionsBox.close();
				_menuPopUp.close();
				_feedbackPopUp.close();
				_magnifier.close();
			}
			
			override public function createReturnObject():Object
			{
				var saveXML:XML;
				var sceneXML:XML;
				var logXML:XML;
				
				if(_currentDocument != null)
				{
					delete(_currentDocument.xml.scene);
				}
				
				sceneXML = _KSketch.sceneXML;
				
				if(!enableLog)
					_KSketch.beginSession();
				
				logXML = _KSketch.sessionLog;
				saveXML = logXML;
				
				if(0 < sceneXML.children().length())
				{
					var thumbnailData:BitmapData = modelDisplay.getThumbnail(0);
					var encoder:JPEGEncoder = new JPEGEncoder();
					var base64Encoder:Base64Encoder = new Base64Encoder();
					base64Encoder.encodeBytes(encoder.encode(thumbnailData));
					
					var thumbnailNode:XML = <thumbnail data=""/>;
					thumbnailNode.@data = base64Encoder.toString();	
					saveXML.appendChild(thumbnailNode);
					
					saveXML.appendChild(sceneXML);
				}
				
				if(0 < logXML.children().length())
				{
					if(!saveXML.log)
						saveXML.appendChild(<log/>);
				}
				
				if(_currentDocument == null)
				{
					//(name:String, xml:XML, id:String,  date:Date, originalName:String, version:int, sketchId:int, description:String = "")
					_currentDocument = new KSketchDocument("",saveXML,"",new Date(),"", 0,-1,"");
				}
				else
				{
					_currentDocument.xml = saveXML;
				}
				
				return _currentDocument;
			}
			
			public function loadFile(readData:XML):void
			{
				_KSketch.reset();
				_interactionControl.reset();
				_interactionControl.dispatchEvent(new Event(KInteractionControl.EVENT_UNDO_REDO));
				
				if(readData.scene.children().length() > 0)
					_KSketch.generateSceneFromXML(new XML(readData.scene));
			}
			
			public function saveTrigger():void
			{
				var xml:XML = saveFile(modelDisplay.getThumbnail(0));
				if(xml)
					dispatchEvent(new KIOEvent(KIOEvent.EVENT_SAVE, xml, true));
			}
			
			public function loadTrigger(fileData:String):void
			{
				loadFile(new XML(fileData));
			}
			
			public function saveFile(bitmapData:BitmapData):XML
			{
				var saveXML:XML = <KSketch date=""/>;
				var sceneXML:XML = _KSketch.sceneXML;
				
				if(0 < sceneXML.children().length())
				{
					var encoder:JPEGEncoder = new JPEGEncoder();
					var base64Encoder:Base64Encoder = new Base64Encoder();
					base64Encoder.encodeBytes(encoder.encode(bitmapData));
					
					var thumbnailNode:XML = <thumbnail data=""/>;
					thumbnailNode.@data = base64Encoder.toString();							
					
					saveXML.appendChild(thumbnailNode);
					saveXML.appendChild(sceneXML);
					saveXML.appendChild(<log/>);
					
					saveXML.@date = new Date().toString();
					return saveXML;
				}
				else
					return null;
			}
			
			public function get regions():Array
			{
				var regionsArr:Array = new Array(6);
				regionsArr[0] = region_1;
				regionsArr[1] = region_2;
				regionsArr[2] = region_3;
				regionsArr[3] = region_4;
				regionsArr[4] = region_5;
				regionsArr[5] = region_6;
				
				return regionsArr;
			}
			
			public function getRegionByIndex(index:int):DisplayObject
			{
				var regionsArr:Array = regions;
				return regionsArr[index-1];
			}
			
			public function setRegionVisibility(visible:Boolean):void
			{
				region_1.visible = visible;
				region_2.visible = visible;
				region_3.visible = visible;
				region_4.visible = visible;
				region_5.visible = visible;
				region_6.visible = visible;
			}
			
			public function setAnimationPlaying(value:Boolean):void
			{
				_activityControl.isAnimationPlaying = value;
			}
			
			public function get isNewSketch():Boolean
			{
				return _activityControl.isNewSketch;
			}
		]]>
	</fx:Script>
	
	<s:Group width="100%" height="100%" id="mainCanvas">
		<s:layout>
			<s:VerticalLayout gap="0" id="mainCanvasLayout"/>
		</s:layout>
		<!-- Timebar-->
		<s:Group id="timebar_group" width="100%"  depth="1">
			<s:filters>
				<s:DropShadowFilter color="{KSketchStyles.TIMEBAR_SHADOW_COLOR}" angle="{KSketchStyles.TIMEBAR_SHADOW_ANGLE}"
									alpha="{KSketchStyles.TIMEBAR_SHADOW_ALPHA}"
									strength="{KSketchStyles.TIMEBAR_SHADOW_STRENGTH}"
									blurX="{KSketchStyles.TIMEBAR_SHADOW_DISTANCE}"
									blurY="{KSketchStyles.TIMEBAR_SHADOW_DISTANCE}"/>
			</s:filters>
			<s:Rect width="100%" height="100%">
				<s:fill>
					<s:SolidColor color="{KSketchStyles.TIMEBAR_BACKGROUND_COLOR}"/>
				</s:fill>
			</s:Rect>
			<s:Group width="100%">
				<s:layout>
					<s:HorizontalLayout
						horizontalAlign="center"
						gap="{KSketchStyles.TIMEBAR_LAYOUT_GAP}"
						paddingLeft="{KSketchStyles.TIMEBAR_LAYOUT_PADDING_HORIZONTAL}"
						paddingRight="{KSketchStyles.TIMEBAR_LAYOUT_PADDING_HORIZONTAL}"
						paddingTop="{KSketchStyles.TIMEBAR_LAYOUT_PADDING_VERTICAL}"
						paddingBottom="{KSketchStyles.TIMEBAR_LAYOUT_PADDING_VERTICAL}"/>
				</s:layout>
				
				<s:Group>
					<s:Group>
						<s:layout><s:VerticalLayout/></s:layout>
						<s:Group>
							<s:layout><s:HorizontalLayout verticalAlign="middle"/></s:layout>
							
							<s:Group>
								<s:layout><s:VerticalLayout/></s:layout>
								<timebar:KSketch_TimeSkip_Button
									id="leftTimeJumper"
									width="{KSketchStyles.TIMEBAR_BUTTON_WIDTH}" height="{KSketchStyles.TIMEBAR_BUTTON_HEIGHT}"/>
							</s:Group>
							
							
							<buttons:KSketch_Play_Button
								doubleClickEnabled="true"
								click="playButton.playToggle()"
								doubleClick="playButton.doubleClick()"
								id="playButton"
								width="{KSketchStyles.TIMEBAR_PLAY_BUTTON_WIDTH}" height="{KSketchStyles.TIMEBAR_PLAY_BUTTON_HEIGHT}"/>
							
							<!--
							<buttons:KSketch_Play_Button
								click="playButton.playToggle()"
								id="playButton"
								width="{KSketchStyles.TIMEBAR_PLAY_BUTTON_WIDTH}" height="{KSketchStyles.TIMEBAR_PLAY_BUTTON_HEIGHT}"/>
							-->
							
							<s:Group>
								<s:layout><s:VerticalLayout/></s:layout>
								<timebar:KSketch_TimeSkip_Button
									id="rightTimeJumper"
									width="{KSketchStyles.TIMEBAR_BUTTON_WIDTH}" height="{KSketchStyles.TIMEBAR_BUTTON_HEIGHT}"/>
							</s:Group>
						</s:Group>
					</s:Group>
				</s:Group>	
				<s:Group width="9"/>
				<timebar:KSketch_TimeControl id="timeControl"/>
				<s:Group width="2"/>
			</s:Group>
		</s:Group>
		<!-- End of Timebar -->
		
		<s:Group id="canvas_component" width="100%" height="100%" depth="0">
			<s:Rect left="-1" right="-1" top="-1" bottom="-1">
				<s:fill>
					<s:SolidColor color="{KSketchStyles.APP_BACKGROUND_COLOR}"/>
				</s:fill>
			</s:Rect>
			<!-- For show, all the visual elements that the user wont be able to "touch"-->
			<s:Group width="100%" height="100%" id="drawingStageGroup">
				<s:layout>
					<s:VerticalLayout horizontalAlign="center" verticalAlign="middle"/>
				</s:layout>
				<s:Group id="drawing_stage" height="92%" maxHeight="{Capabilities.screenResolutionX*0.7}"
						 width="{drawing_stage.height/10*16}">
					<s:Rect width="100%" height="100%">
						<s:fill>
							<s:SolidColor color="{KSketchStyles.CANVAS_PAPER_COLOR}"/>
						</s:fill>
						<s:stroke>
							<s:SolidColorStroke color="{KSketchStyles.CANVAS_BORDER_COLOR}"
												weight="{KSketchStyles.CANVAS_BORDER_WEIGHT}"/>
						</s:stroke>
						<s:filters>
							<s:DropShadowFilter 
								color="{KSketchStyles.CANVAS_SHADOW_COLOR}" angle="{KSketchStyles.CANVAS_SHADOW_ANGLE}"
								alpha="{KSketchStyles.CANVAS_SHADOW_ALPHA}"
								strength="{KSketchStyles.CANVAS_SHADOW_STRENGTH}"
								blurX="{KSketchStyles.CANVAS_SHADOW_X_DISTANCE}"
								blurY="{KSketchStyles.CANVAS_SHADOW_Y_DISTANCE}"/>
						</s:filters>
					</s:Rect>
					<!-- start of region -->
					<s:Group height="100%" width="100%" alpha="0.5">
						<s:layout>
							<s:VerticalLayout/>
						</s:layout>
						<s:Group height="100%" width="100%">
							<s:layout>
								<s:HorizontalLayout/>
							</s:layout>
							<s:Group id="region_1" width="100%" height="100%">
								<s:Rect width="100%" height="100%">
									<s:fill>
										<s:SolidColor color="{KSketchStyles.CANVAS_PAPER_REGION}"/>
									</s:fill>
								</s:Rect>
							</s:Group>
							<s:Group id="region_2" width="100%" height="100%">
								<s:Rect width="100%" height="100%">
									<s:fill>
										<s:SolidColor color="{KSketchStyles.CANVAS_PAPER_REGION}"/>
									</s:fill>
								</s:Rect>
							</s:Group>
							<s:Group id="region_3" width="100%" height="100%">
								<s:Rect width="100%" height="100%">
									<s:fill>
										<s:SolidColor color="{KSketchStyles.CANVAS_PAPER_REGION}"/>
									</s:fill>
								</s:Rect>
							</s:Group>
						</s:Group>
						<s:Group height="100%" width="100%">
							<s:layout>
								<s:HorizontalLayout/>
							</s:layout>
							<s:Group id="region_4" width="100%" height="100%">
								<s:Rect width="100%" height="100%">
									<s:fill>
										<s:SolidColor color="{KSketchStyles.CANVAS_PAPER_REGION}"/>
									</s:fill>
								</s:Rect>
							</s:Group>
							<s:Group id="region_5" width="100%" height="100%">
								<s:Rect width="100%" height="100%">
									<s:fill>
										<s:SolidColor color="{KSketchStyles.CANVAS_PAPER_REGION}"/>
									</s:fill>
								</s:Rect>
							</s:Group>
							<s:Group id="region_6" width="100%" height="100%">
								<s:Rect width="100%" height="100%">
									<s:fill>
										<s:SolidColor color="{KSketchStyles.CANVAS_PAPER_REGION}"/>
									</s:fill>
								</s:Rect>
							</s:Group>
						</s:Group>
					</s:Group>
					<!-- end of region -->
					<!-- The model display displays the objects in the model -->
					<view:KMotionDisplay id="motionDisplay" scaleX="{drawing_stage.width/KSketch2.CANONICAL_WIDTH}"
										 scaleY="{drawing_stage.height/KSketch2.CANONICAL_HEIGHT}"/>
					<view:KModelDisplay id="modelDisplay" scaleX="{drawing_stage.width/KSketch2.CANONICAL_WIDTH}"
										scaleY="{drawing_stage.height/KSketch2.CANONICAL_HEIGHT}"/>
				</s:Group> 
			</s:Group>
			
			<!-- Things inside this group really dispatches gesture events-->
			<s:Group width="100%" height="100%">
				<s:Group id="canvas_region" width="100%" height="100%">
					<s:Rect width="100%" height="100%" alpha="0">
						<s:fill>
							<s:SolidColor color="0x000000"/>
						</s:fill>
					</s:Rect>
				</s:Group>
				<transformWidget2:KSketch_Widget_Component id="widget" visible="false"/>
			</s:Group>
		</s:Group>
	</s:Group>
</s:View>