<?xml version="1.0" encoding="utf-8"?>
<!--
*Copyright 2010-2012 Singapore Management University
*Developed under a grant from the Singapore-MIT GAMBIT Game Lab

*This Source Code Form is subject to the terms of the
*Mozilla Public License, v. 2.0. If a copy of the MPL was
*not distributed with this file, You can obtain one at
*http://mozilla.org/MPL/2.0/.
-->
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009" 
		xmlns:s="library://ns.adobe.com/flex/spark" 
		xmlns:mx="library://ns.adobe.com/flex/mx" 
		actionBarVisible="false" 
		xmlns:view="sg.edu.smu.ksketch2.canvas.components.view.*"
		xmlns:buttons="sg.edu.smu.ksketch2.canvas.components.buttons.*" 
		xmlns:popup="sg.edu.smu.ksketch2.canvas.components.popup.*"
		initialize="init()"
		addedToStage="startMonitorConnection()"
		viewActivate="init()"
		destructionPolicy="never"
		>
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
		<s:HTTPService id="GetUserSketch"               
					   resultFormat="text" 
					   showBusyCursor="false">                        
		</s:HTTPService>
	</fx:Declarations>
	<fx:Script>
		<![CDATA[
			import com.adobe.serialization.json.JSON;
			
			import mx.collections.ArrayCollection;
			import mx.events.FlexEvent;
			import mx.managers.PopUpManager;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			import mx.utils.ObjectProxy;
			
			import spark.events.IndexChangeEvent;
			
			import air.net.URLMonitor;
			
			import sg.edu.smu.ksketch2.KSketchAssets;
			import sg.edu.smu.ksketch2.KSketchStyles;
			import sg.edu.smu.ksketch2.KSketchWebLinks;
			import sg.edu.smu.ksketch2.KSketch_User;
			import sg.edu.smu.ksketch2.KSketch_UserSketches;
			import sg.edu.smu.ksketch2.canvas.KSketch_LoginView;
			import sg.edu.smu.ksketch2.canvas.components.popup.KSketch_CanvasNotice;
			import sg.edu.smu.ksketch2.canvas.components.popup.KSketch_LogoutNotice;
			import sg.edu.smu.ksketch2.canvas.components.popup.KSketch_SaveOptions;
			import sg.edu.smu.ksketch2.canvas.components.popup.KSketch_SyncOptions;
			import sg.edu.smu.ksketch2.canvas.controls.KFileControl;
			import sg.edu.smu.ksketch2.canvas.controls.KMobileControl;
			import sg.edu.smu.ksketch2.canvas.controls.KWebControl;
			import sg.edu.smu.ksketch2.utils.KSketchDocument;
			import sg.edu.smu.ksketch2.utils.KSwipeEvent;
			import sg.edu.smu.ksketch2.utils.KWebData;
			
			private static const DISPLAY_FAILED_LOGIN:int = 0;
			private static const DISPLAY_FAILED_SYNC:int = 1;
			
			[Bindable]
			private var arrDG:ArrayCollection = new ArrayCollection();
			
			public static var autoSaveCounter:int = 0;
			
			//class variables
			public var userData:Object = new Object();
			private var _stage:Stage;
			private var _mySO:SharedObject;
			private var _resultSketch:String;
			
			//variables for other components
			public var _webControl:KWebControl;
			public var _mobileControl:KMobileControl;
			private var _fileControl:KFileControl;
			private var _syncOptions:KSketch_SyncOptions;
			private var _logoffNotice:KSketch_LogoutNotice;
			private var _canvasNotice:KSketch_CanvasNotice;
			
			//variables for connection
			public var _isConnected:Boolean;
			private var _viewTimer:Timer;
			private var _monitor:URLMonitor;
			private var _monitorTimer:Timer;
			private var _failedLogin:Boolean = false;
			private var _lostLogin:Boolean = false;
			
			//progress bar variables
			private var progressTimer:Timer;
			
			//fail notifications for login and sync variables
			private var canvasTimer:Timer;
			private var displayFailedMessage:int = 0;				//display failed message variable (0) for login, (1) for sync
			public static var canvasTimerDisabled:Boolean = false;
			public static var hasBeenNotified:Boolean = false;
			
			//list of sketches variables
			private var _oldEvent:Event;
			private var _isXML:Boolean = false;
			private var _deleteSketchFlag:Boolean = false;
			private var _selectedSketchName:String;
			private var _selectedSketchId:String;
			private var _selectedSketchVersion:String;
			
			private function timerComplete(event:TimerEvent):void
			{
				canvasTimer.removeEventListener(TimerEvent.TIMER, timerComplete);
				canvasTimer.stop();
				
				canvasCover.visible = false;
				canvasTimerDisabled = true;
			}
			
			private function init():void
			{	
				if(stage.deviceOrientation == StageOrientation.UNKNOWN)
				{
					if(KSketch_SplashView.initialOrientation == StageOrientation.DEFAULT || 
						KSketch_SplashView.initialOrientation == StageOrientation.UPSIDE_DOWN)
						stage.setOrientation(StageOrientation.ROTATED_RIGHT);
					else
						stage.setOrientation(KSketch_SplashView.initialOrientation);
				}
				
				
				if(!canvasTimerDisabled)
				{
					canvasTimer = new Timer(2000);
					canvasTimer.addEventListener(TimerEvent.TIMER, timerComplete);
					canvasTimer.start();	
				}
				else
					canvasCover.visible = false;
				
				_fileControl = new KFileControl();
				
				_syncOptions = new KSketch_SyncOptions();
				_syncOptions.createDeferredContent();
				
				_logoffNotice = new KSketch_LogoutNotice();
				_logoffNotice.createDeferredContent();
				
				_canvasNotice = new KSketch_CanvasNotice();
				_canvasNotice.createDeferredContent();
				
				//set KSketch Logo
				logoHolder.init(KSketchAssets.texture_logo, KSketchAssets.texture_logo);
				
				GetUserSketch.addEventListener(FaultEvent.FAULT, retrieveFaultHandler);
				GetUserSketch.addEventListener(ResultEvent.RESULT, retrieveResultHandler);
				stage.addEventListener(KSwipeEvent.DELETE_ACTION, listSketches_deleteSketchHandler);
			}
			
			private function prepareHomeView():void
			{
				//initialize login button and KUser object
				if(!data || data == "Skip Login" || data == "Lost Connection" || data == "Janrain Failed")
				{
					if(data)
					{
						var errorMessage:String = String(data);
						
						if(errorMessage == "Lost Connection" || errorMessage == "Janrain Failed") 
						{
							startDisplayFailedNotification(DISPLAY_FAILED_LOGIN, 500);
							
							hasBeenNotified = true;
							_failedLogin = true;
						}
					}
					
					var obj:Object = new Object();
					obj.status = "failed";
					obj.u_login = "n.a";
					var kUser:KSketch_User = new KSketch_User(obj);
					userData.kUser = kUser;
					
					loginButton.label = "Login";
				}
				else
				{
					//cast data from previous view to ObjectProxy. This is to avoid getting IEventDispatcher error
					var dataProxy:ObjectProxy = new ObjectProxy(data);
					
					if(dataProxy.kUser)
						userData.kUser = dataProxy.kUser;
					
					if(KSketch_LoginView._isLoggedIn)
					{
						loginButton.label = "Logout, "+ userData.kUser.u_realname;
						
						if(!_isConnected && !hasBeenNotified)
						{
							startDisplayFailedNotification(DISPLAY_FAILED_SYNC, 2500);
							hasBeenNotified = true;
						}
					}
						
					else
						loginButton.label = "Login";
				}
				
				createButton.label = "Create a Sketch";
				
				prepareDataList();
			}
			
			public function prepareDataList():void
			{
				startProgressBar();
				
				_mobileControl = new KMobileControl();
				
				userData.homeView = this;
				userData.mobileControl = _mobileControl;
				userData.kSketchDocument = null;
				
				if(KSketch_LoginView._isLoggedIn)			//logged in
				{
					if(_isConnected)
					{
						_webControl = new KWebControl(userData.kUser, _mobileControl);
						//create a buffer time for scene to prepare itself, implement timer
						_viewTimer = new Timer(1000,1);
						_viewTimer.addEventListener(TimerEvent.TIMER_COMPLETE, loadConnected);
						_viewTimer.start();
					}
					else
						displaySketchData();				//anonymous
				}
				else										//anonymous
					displaySketchData();
			}
			
			private function loadConnected(event:TimerEvent):void
			{
				_viewTimer.stop();
				_viewTimer.removeEventListener(TimerEvent.TIMER_COMPLETE, loadConnected);
				resolveConflict();	
			}
			
			public function updateFromWeb():void
			{
				_isXML = false;						//JSON retrieval is list of sketches, not XML of sketch
				
				//get json object of user's list of sketches from the URL
				//returned result will be a stringified JSON object
				GetUserSketch.url = KSketchWebLinks.jsonurlSketch + userData.kUser.id;
				GetUserSketch.send();
			}
			
			private function retrieveResultHandler(event:ResultEvent):void
			{
				var rawData:String = String(event.result);
				var resultObj:Object = com.adobe.serialization.json.JSON.decode(rawData,true);
				
				if(!_isXML && !_deleteSketchFlag)
				{
					var syncSketchArr:ArrayCollection;
					if(resultObj)
					{
						var newArr:ArrayCollection = new ArrayCollection();
						var tempArr:Array = (resultObj.entities as Array);
						if(tempArr.length > 0)
						{
							var newTempArr:ArrayCollection = _fileControl.convertArrayToArrayCollection(tempArr);
							for each(var tempObj:Object in newTempArr)
							{
								tempObj = KWebData.convertWebObjForMobile(tempObj);
								newArr.addItem(tempObj);
							}
							
							for each(var obj:Object in newArr)
							_webControl.addSketchToList(obj, "save");
							
						}
					}	
					
					reinitializeCache();
				}
				
				if(_isXML && !_deleteSketchFlag)
				{
					resultObj = KWebData.convertWebObjForMobile(resultObj);
					displaySketchDocument(resultObj, true);
				}
				
				if(_deleteSketchFlag)
				{
					_deleteSketchFlag = false;
					startMonitorConnection();
				}
			}
			
			private function retrieveFaultHandler(event:FaultEvent):void
			{
				if(!_isXML && !_deleteSketchFlag)
					displaySketchData();
				else if (_isXML && !_deleteSketchFlag)
					displaySketchDocument(null, false);
				else if(_deleteSketchFlag)
					deleteSketchDocument(null, "deleteFromWeb");
			}
			
			private function resolveConflict():void
			{
				var syncSketchArr:ArrayCollection = new ArrayCollection();	//array for list of sketches to sync
				var deletedSketchArr:ArrayCollection = new ArrayCollection();
				
				if(_mobileControl.informationArr[1])
				{
					syncSketchArr = _fileControl.getSyncSketchObjects(_mobileControl.informationArr[1], false);
					deletedSketchArr = _fileControl.getSyncSketchObjects(_mobileControl.informationArr[1], true);
				}
					
				//if there are sketches to sync, then prompt dialog
				//else just display data
				if(syncSketchArr.length > 0 || deletedSketchArr.length > 0)
				{
					if(syncSketchArr.length > 0)
					{
						_syncOptions.resetSyncArrays();
						
						_syncOptions.currentUser = userData.kUser;
						_syncOptions.syncSketchArr = syncSketchArr;
						_syncOptions.open(this,true);
						PopUpManager.centerPopUp(_syncOptions);	
					}
					
					if(deletedSketchArr.length > 0)
						_syncOptions.deleteSketch(deletedSketchArr, this);
				}
				else
					updateFromWeb();
			}
			
			private function reinitializeCache():void
			{
				var newInformationArr:String = _fileControl.solveDiscrepancy(_mobileControl.informationArr[1], _webControl.informationArr[1]);
				_mobileControl.informationArr[1] = newInformationArr;
				
				//reset webcontrol
				_webControl.reset();
				
				displaySketchData();
			}
			
			public function displaySketchData():void
			{
				arrDG.removeAll();													//reset arrDG
				var sketchArr:ArrayCollection = new ArrayCollection();				//array for list of sketches from web or mobile
				
				if (_mobileControl && _mobileControl.sketchList)
					sketchArr.addAll(_mobileControl.sketchList);	//display everything from mobile control
				
				if(sketchArr.length > 0)
				{
					var userSketch:KSketch_UserSketches = new KSketch_UserSketches();
					userSketch.createArray(sketchArr, userData.kUser.id);
					
					autoSaveCounter = userSketch.initializeAutoSaveSketchName(userSketch);
					
					//Set DataGrid array to user sketches collection
					var sortBy:String = "sketchId";
					if(!arrDG)
						arrDG = userSketch.getUserSketchArray(sortBy); 
					else
						arrDG.addAll(userSketch.getUserSketchArray(sortBy));
					
					listSketches_unavailable.visible = false;	
					
				}
				else
				{
					autoSaveCounter = 0;
					startNewSketch();
				}
				
				stopProgressBar();
			}
			
			private function startDisplayFailedNotification(input:int, time:Number):void
			{
				displayFailedMessage = input;
				canvasTimer = new Timer(time);
				canvasTimer.addEventListener(TimerEvent.TIMER, displayFailedNotification);
				canvasTimer.start();
			}
			
			private function displayFailedNotification(event:TimerEvent):void
			{
				canvasTimer.stop();
				canvasTimer.removeEventListener(TimerEvent.TIMER, displayFailedNotification);
				
				if(displayFailedMessage == 0)
					_canvasNotice._parent = KSketch_CanvasNotice.CANVASNOTICE_FAILED_LOGIN;
				else if(displayFailedMessage == 1)
					_canvasNotice._parent = KSketch_CanvasNotice.CANVASNOTICE_FAILED_SYNC;
				
				_canvasNotice.open(this, true);
				PopUpManager.centerPopUp(_canvasNotice);
				
				_failedLogin = false;
				displayFailedMessage = 0;
			}
			
			private function displaySketchDocument(obj:Object, checkConflict:Boolean):void
			{
				var finalObj:Object = obj;
				
				var sketchDocArr:ArrayCollection;
				sketchDocArr = _mobileControl.sketchList;
				
				var i:int;
				var tempObj:Object;
				
				if(sketchDocArr)
				{
					for(i=0; i<sketchDocArr.length; i++)
					{
						tempObj = sketchDocArr.getItemAt(i);
						
						if((tempObj.fileName == _selectedSketchName) && (tempObj.sketchId == _selectedSketchId))
						{
							if(!obj || (obj && tempObj.save == -1))
							{
								finalObj = tempObj;
								break;	
							}
						}
					}
				}
				
				if(finalObj.fileData)
				{
					_mobileControl.addSketchToList(finalObj, "saveCurrentFromWeb");
					
					//create XML and date object from JSON to create a KSketchDocument object
					var xml:XML = new XML(finalObj.fileData);
					var date:Date = new Date(finalObj.modified);
					//(name:String, xml:XML, id:String,  date:Date, originalName:String, version:int, sketchId:int, description:String = "")
					var docObj:KSketchDocument = new KSketchDocument(finalObj.fileName, xml, finalObj.id, date, finalObj.originalName, finalObj.version, finalObj.sketchId, finalObj.changeDescription);
					userData.kSketchDocument = docObj;
					navigateToScreen("CANVAS");
				}
				else
				{
					_canvasNotice._parent = KSketch_CanvasNotice.CANVASNOTICE_FAILED_SKETCH;
					_canvasNotice.open(this, true);
					PopUpManager.centerPopUp(_canvasNotice);
				}
				
				_isXML = false;
				
				stopProgressBar();
			}
			
			private function startNewSketch():void
			{
				listSketches_unavailable.visible = true;
			}
			
			private function createSketch():void
			{
				KSketch_CanvasView.createNewSketch = true;
				navigateToScreen('CANVAS');
			}
			
			public function navigateToScreen(screenName:String):void
			{
				resetDatagridSelectItem();
				
				_mobileControl.writeToCache(_mobileControl.informationArr);
				
				if(screenName.indexOf("CANVAS") == -1) //if screen name is Login
				{
					KSketch_LoginView.userData = new Object();
					
					//if this is a logout button, then reset view & delete cache
					if(loginButton.label.indexOf("Login") == -1)
					{
						if(!_fileControl.unsavedSketchExist(_mobileControl.informationArr[1]))
							logout();
						else
						{
							_logoffNotice.open(this,true);
							PopUpManager.centerPopUp(_logoffNotice);	
						}
					}
					else
						navigator.pushView(KSketch_LoginView);	
				}	
				else //if screen name is Canvas
				{
					Multitouch.inputMode = MultitouchInputMode.TOUCH_POINT;
					navigator.pushView(KSketch_CanvasView, userData);
				}
			}
			
			public function logout():void
			{
				KSketch_LoginView._isLoggedIn = false; 
				KSketch_SaveOptions.saveCounter = 0;
				data = null;
				_mobileControl.reset();
				_mobileControl.writeToCache(_mobileControl.informationArr);
				_mobileControl = null;
				_webControl = null;
				
				startMonitorConnection();
			}
			
			public function startMonitorConnection():void
			{
				//check for network connection
				_monitor = new URLMonitor(new URLRequest(KSketchWebLinks.url));
				_monitor.addEventListener(StatusEvent.STATUS, monitorConnection);
				_monitor.start();
				
				_monitorTimer = new Timer(2000);
				_monitorTimer.addEventListener(TimerEvent.TIMER, endMonitor);
				_monitorTimer.start();
			}
			
			private function endMonitor(event:TimerEvent):void
			{
				_monitor.stop();
				_monitorTimer.stop();
				
				_isConnected = false;
				
				_monitor.removeEventListener(StatusEvent.STATUS, monitorConnection);
				_monitorTimer.removeEventListener(TimerEvent.TIMER, endMonitor);
				
				if(_isXML)
					prepareCanvasView();
				else
					prepareHomeView();
			}
			
			private function monitorConnection(event:StatusEvent):void
			{
				if(_monitor.available)
				{
					_isConnected = true;
					hasBeenNotified = false;
				}
				else
					_isConnected = false;
				
				_monitor.stop();
				_monitorTimer.stop();
				
				_monitor.removeEventListener(StatusEvent.STATUS, monitorConnection);
				_monitorTimer.removeEventListener(TimerEvent.TIMER, endMonitor);
				
				if(_isXML)
					prepareCanvasView();
				else if(_deleteSketchFlag)
					prepareDeleteSketch();
				else
					prepareHomeView();
			}
			
			private function startProgressBar():void
			{
				progressBar.visible = true;
				
				progressTimer = new Timer(1);
				progressTimer.addEventListener(TimerEvent.TIMER, progressBarHandler);
				progressTimer.start();
			}
			
			private function stopProgressBar():void
			{
				//stop progress bar
				progressTimer.stop();
				progressTimer.removeEventListener(TimerEvent.TIMER, progressBarHandler);
				progressBar.visible = false;
			}
			
			private function progressBarHandler(event:TimerEvent):void
			{
				progressBar.value += .008;
				
				if (progressBar.value >= 1)
					progressBar.value = 0;
			}
			
			private function prepareCanvasView():void
			{
				if(_isConnected)
				{
					var finalSketchVersion:String = _selectedSketchVersion;
					
					//retrieve the latest version from the web
					if(KSketch_LoginView._isLoggedIn && _isConnected)
						finalSketchVersion = -1 + "";	
					
					//get json object of sketch from the URL and according to sketchid and version number
					GetUserSketch.url = KSketchWebLinks.jsonurlSketchXML + "/" + _selectedSketchId + "/" + finalSketchVersion + "/" + userData.kUser.id; //selectedSketchVersion;
					GetUserSketch.send();
					startProgressBar();	
				}
				else
				{
					displaySketchDocument(null, false);
				}
				
			}
			
			//cammie: need to implement delete sketch on python
			private function prepareDeleteSketch():void
			{
				if(_isConnected && KSketch_LoginView._isLoggedIn)
				{
					if(_selectedSketchId && (parseInt(_selectedSketchId) > -1))
					{
						var objToSend:Object = new Object();
						objToSend["sketchid"] = parseInt(_selectedSketchId);
						objToSend["userid"] = userData.kUser.id;
						
						//get json object of sketch from the URL and according to sketchid and version number
						GetUserSketch.url = KSketchWebLinks.jsonurlDeleteSketch;
						GetUserSketch.send(objToSend);
						
						startProgressBar();	
					}
					
					deleteSketchDocument(null, "deleteFromCache");
				}
				else if(KSketch_LoginView._isLoggedIn)
					deleteSketchDocument(null, "deleteFromWeb");
				else
					deleteSketchDocument(null, "deleteFromCache");
			}
			
			public function deleteSketchDocument(obj:Object, type:String):void
			{
				//close action bar
				if(_oldEvent)
				{
					KSketch_HomeView_IconItemRenderer.openActionBar = false;
					(_oldEvent.target as KSketch_HomeView_IconItemRenderer).removeElement((_oldEvent.target as KSketch_HomeView_IconItemRenderer).actBar);
					_oldEvent = null;
				}
				
				var finalObj:Object = obj;
				
				var sketchDocArr:ArrayCollection;
				sketchDocArr = _mobileControl.sketchList;
				
				var i:int;
				var tempObj:Object;
				
				if(sketchDocArr)
				{
					for(i=0; i<sketchDocArr.length; i++)
					{
						tempObj = sketchDocArr.getItemAt(i);
						
						if((tempObj.fileName == _selectedSketchName) && (tempObj.sketchId == _selectedSketchId))
						{
							if(_selectedSketchId == "")
								type = "deleteFromCache";
							finalObj = tempObj;
							break;	
						}
					}
				}
				
				if(finalObj)
					_mobileControl.deleteSketchFromList(finalObj, type);
				
				_deleteSketchFlag = false;
				stopProgressBar();
				startMonitorConnection();
			}
			
			protected function listSketches_mouseDownHandler(event:MouseEvent):void
			{
				//TODO Auto-generated method stub
				if(!_oldEvent)
					_oldEvent = event;
				else
				{
					var currentSelectedSketchName:String;
					if(event.target)
					{
						if(event.target as KSketch_HomeView_IconItemRenderer)
						{
							if((_oldEvent.target as KSketch_HomeView_IconItemRenderer) != (event.target as KSketch_HomeView_IconItemRenderer))
							{
								currentSelectedSketchName = (event.target as KSketch_HomeView_IconItemRenderer).sketchName.text;
								
								if((_oldEvent.target as KSketch_HomeView_IconItemRenderer).actBar)
									if((_oldEvent.target as KSketch_HomeView_IconItemRenderer).actBar.stage)
									{
										KSketch_HomeView_IconItemRenderer.openActionBar = false;
										(_oldEvent.target as KSketch_HomeView_IconItemRenderer).removeElement((_oldEvent.target as KSketch_HomeView_IconItemRenderer).actBar);
									}
								
								_oldEvent = event;
							}
						}
					}	
				}
			}
			
			protected function listSketches_changeSelectedHandler(event:Event):void
			{
				_selectedSketchName = KSketch_HomeView_IconItemRenderer.selectedSketchName;
				_selectedSketchId = KSketch_HomeView_IconItemRenderer.selectedSketchId;
				_selectedSketchVersion = KSketch_HomeView_IconItemRenderer.selectedVersion;
				
				//if action bar is displayed, reset selection to -1
				if(	KSketch_HomeView_IconItemRenderer.openActionBar)
					listSketches.selectedIndex = -1;
				
				//if action bar is not displayed and there is no additional flag
				if(!KSketch_HomeView_IconItemRenderer.openActionBar && !_deleteSketchFlag)
					_isXML = true;
					
				if(_isXML || _deleteSketchFlag)
					startMonitorConnection();
			}
			
			protected function listSketches_deleteSketchHandler(event:KSwipeEvent):void
			{
				_deleteSketchFlag = true;
			}
			
			private function resetDatagridSelectItem():void
			{
				arrDG.removeAll();
				_selectedSketchName = null;
				_selectedSketchId = null;
				_selectedSketchVersion = null;
				KSketch_HomeView_IconItemRenderer.selectedSketchName = null;
				KSketch_HomeView_IconItemRenderer.selectedSketchId = null;
				KSketch_HomeView_IconItemRenderer.selectedVersion = null;
			}
			
		]]>
	</fx:Script>
	<s:Group width="100%" height="100%">
		
		<!-- start of first layer -->
		<s:Group width="100%" height="100%">
			<!-- defines the page to arrange 'group' elements from left to right -->
			<s:layout> <s:HorizontalLayout gap="2"/> </s:layout>
			
			<!-- First half of the page -->
			<s:Group width="33%" height="100%">
				<!-- sets the background to grey -->
				<s:Rect width="100%" height="100%">
					<s:fill> <s:SolidColor color="0xE7E7E7"/> </s:fill>
				</s:Rect>
				
				<s:Group width="100%" height="100%">
					<!-- defines the page to arrange 'group' elements from top to bottom -->
					<s:layout> <s:VerticalLayout paddingLeft="15" paddingRight="15" paddingTop="15"/> </s:layout>
					
					<!-- Alignment for logo button -->
					<s:Group width="100%" height="40%">
						<s:layout> <s:HorizontalLayout paddingLeft="48" paddingTop="10"/> </s:layout>
						<buttons:KSketch_Button id="logoHolder"
												width="{KSketchStyles.LOGO_BUTTON_WIDTH}" 
												height="{KSketchStyles.LOGO_BUTTON_HEIGHT}"
												click="startMonitorConnection()"/>
					</s:Group>
					
					<s:Group width="100%">
						<s:layout> <s:VerticalLayout paddingBottom="10"/> </s:layout>
						<s:Button id="createButton" width="100%" click="createSketch()"/>
					</s:Group>
					
					<s:Group width="100%">
						<s:layout> <s:VerticalLayout paddingBottom="10"/> </s:layout>
						<s:Button id="loginButton" width="100%" click="navigateToScreen('LOGIN')"/>
					</s:Group>
					
					<!-- end alignment for buttons -->
				</s:Group>
			</s:Group>
			<!-- end of first half -->
			
			<!-- Second half of the page -->
			<s:Group width="67%" height="100%">
				
				<!-- Background for previewer -->
				<s:Rect width="100%" height="100%">
					<s:fill>
						<s:SolidColor color="0xE7E7E7"/>
					</s:fill>
				</s:Rect>
				
				<!--Previewer portion-->
				<s:Group width="100%" height="100%">
					<s:Rect top="15" left="15" right="15" bottom="15">
						<s:fill>
							<s:SolidColor color="0xFFFFFF"/>
						</s:fill>
						<s:filters>
							<s:DropShadowFilter angle="90" distance="1" color="0xdfdfdf"/>
						</s:filters>
					</s:Rect>
					
					<s:Group top="15" left="15" right="15" bottom="15">
						<s:List id="listSketches" contentBackgroundAlpha="0" width="100%" height="100%"
								itemRenderer="sg.edu.smu.ksketch2.canvas.components.view.KSketch_HomeView_IconItemRenderer"
								dataProvider="{arrDG}" horizontalScrollPolicy="off" mouseDown="listSketches_mouseDownHandler(event)" change="listSketches_changeSelectedHandler(event)">
						</s:List>
						<s:Image id="listSketches_unavailable" source="{KSketchAssets.image_new}" width="100%" height="100%" visible="false"/>
						<popup:DataProgressBar id="progressBar" skinClass="sg.edu.smu.ksketch2.canvas.components.popup.KSketch_DataProgressBar"
											   horizontalCenter="0" verticalCenter="0" visible="false"/>
					</s:Group>
					
				</s:Group> 
				<!-- end of previewer portion-->
			</s:Group>	
			<!-- end of second half -->
		</s:Group>
		<!-- end of first layer -->
		
		<!-- start of second layer -->
		<s:Group id="canvasCover" width="100%" height="100%" visible="true">
			<s:Rect  width="100%" height="100%">
				<s:fill>
					<s:SolidColor color="0xffffff"/>
				</s:fill>
			</s:Rect>
		</s:Group>
		<!-- end of second layer -->
	</s:Group>
</s:View>
