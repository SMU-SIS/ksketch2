<?xml version="1.0" encoding="utf-8"?>
<!--
*Copyright 2010-2012 Singapore Management University
*Developed under a grant from the Singapore-MIT GAMBIT Game Lab

*This Source Code Form is subject to the terms of the
*Mozilla Public License, v. 2.0. If a copy of the MPL was
*not distributed with this file, You can obtain one at
*http://mozilla.org/MPL/2.0/.
-->
<s:BorderContainer xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" width="100%" height="25" borderWeight="0" 
		 xmlns:timebar="sg.edu.smu.ksketch2.components.timebar.*">
	<fx:Script>
		<![CDATA[
			import mx.controls.sliderClasses.Slider;
			import mx.utils.OnDemandEventDispatcher;
			
			import sg.edu.smu.ksketch.event.KModelEvent;
			import sg.edu.smu.ksketch.logger.KLogger;
			import sg.edu.smu.ksketch.model.IKeyFrame;
			import sg.edu.smu.ksketch.model.ISpatialKeyframe;
			import sg.edu.smu.ksketch.model.KModel;
			import sg.edu.smu.ksketch.operation.IModelOperation;
			import sg.edu.smu.ksketch.operation.KModelFacade;
			import sg.edu.smu.ksketch.utilities.KAppState;
			
			public static const OVERVIEW:int = 0;
			public static const TRANSLATE:int = 1;
			public static const ROTATE:int = 2;
			public static const SCALE:int = 3;
			public static const ACTIVITY:int = 4;
			public static var pixelPerFrame:Number;
			
			//Widget atrributes
			private var _appState:KAppState;
			private var _facade:KModelFacade;
			private var _slider:TimeSlider;
			private var _type:int;
			private var _futureConstant:Boolean = true;
			
			//Variables associated with dragging.
			private var _targetMarker:Marker;
			private var _startX:Number;
			private var _currentX:Number;
			private var _changeX:Number;
			
			private var _activityBars:Vector.<ActivityBar>;
			private var _selectedMarkers:Vector.<Marker>;
			private var _unselectedMarkers:Vector.<Marker>;
			
			public function initTimeWidget(facade:KModelFacade, appState:KAppState, slider:TimeSlider, type:int = -1):void
			{
				_facade = facade;
				_appState = appState;
				_type = type;
				pixelPerFrame = 1;
				_slider = slider;
			}
			
			/**
			 * Clears the current set of markers in the widget
			 * Retrieves required information from the facade
			 * Renders a new set of markers using the information retrieved
			 */
			public function updateTimeWidget(markers:Vector.<Marker>):void
			{
				var widgetMaxTime:Number = 0;
				calibrateTimeWidget();
				_activityBars = new Vector.<ActivityBar>();
				_selectedMarkers = new Vector.<Marker>();
				_unselectedMarkers = new Vector.<Marker>();
				
				var marker:Marker;
				for each(marker in markers)
				{
					if(_type == TimeWidget.OVERVIEW)
						if(widgetMaxTime < marker.time)
							widgetMaxTime = marker.time;
		
					marker.x  = _timeToPosition(marker.time);
					if(marker.x < 0)
						continue;
					marker.addEventListener(MouseEvent.MOUSE_DOWN,_marker_MouseDown, false, 0, true);
					if(marker.selected)
					{
						selectedMarkerContainer.addElement(marker);
						_selectedMarkers.push(marker);
					}
					else
					{
						markerContainer.addElement(marker);
						_unselectedMarkers.push(marker);
					}

					marker.originalPosition = marker.x;
				}
				
				if(_type == TimeWidget.OVERVIEW)
					_appState.maxTime = _computeAppMaxTime(widgetMaxTime);
				
				rescaleTimeWidget();

				
				//Sort and form a 2 different linked lists of
				//selected or unselected markers
				var i:int;
				
				_selectedMarkers.sort(_compareMarkerPosition)
				for(i = 1; i< _selectedMarkers.length; i++)
				{
					_selectedMarkers[i].previous = _selectedMarkers[i-1];
					_selectedMarkers[i-1].next = _selectedMarkers[i];
				}
				
				_unselectedMarkers.sort(_compareMarkerPosition);
				for(i = 1; i< _unselectedMarkers.length; i++)
				{
					_unselectedMarkers[i].previous = _unselectedMarkers[i-1];
					_unselectedMarkers[i-1].next = _unselectedMarkers[i];
				}
				
				_generateActivityBars(_selectedMarkers, true);
				_generateActivityBars(_unselectedMarkers, false);
				_renderBars();
			}
			
			/**
			 * Positions the markers in the widget according to the width of the time slider
			 * using the current set of markers
			 */
			public function rescaleTimeWidget():void
			{
				var prevPixelPerFrame:Number = pixelPerFrame;
				calibrateTimeWidget();

				var numMarkers:int = markerContainer.numElements;
				var i:int = 0;
				var currentMarker:Marker;
				var frame:Number;
				
				for(i; i < numMarkers; i++)
				{
					currentMarker = markerContainer.getElementAt(i) as Marker;
					frame = currentMarker.x /prevPixelPerFrame;
					currentMarker.x = frame * pixelPerFrame;
					currentMarker.originalPosition = currentMarker.x;
				}
			
				numMarkers = selectedMarkerContainer.numElements;
				
				for(i=0; i < numMarkers; i++)
				{
					currentMarker = selectedMarkerContainer.getElementAt(i) as Marker;
					frame = currentMarker.x /prevPixelPerFrame;
					currentMarker.x = frame * pixelPerFrame;
					currentMarker.originalPosition = currentMarker.x;
				}
				
				_renderBars();
			}
			
			/**
			 * Set the time/pixel for this time widget
			 */
			public function calibrateTimeWidget():void
			{
				pixelPerFrame = (_slider.width-_slider.thumb.width)/_slider.maximum;
				_appState.pixelPerFrame = pixelPerFrame;
			}
			
			public function updateBackground(width:Number):void
			{
				bgFill.width = width;
			}
			
			/**
			 * Function to convert time (in miliseconds) to x position in the widget.
			 */
			private function _timeToPosition(time:Number):Number
			{
				return KAppState.indexOf(time) * pixelPerFrame;
			}
			
			/**
			 * Function to convert position(pixels) to frame time in miliseconds
			 */
			private function _positionToTime(xPos:Number):Number
			{
				return KAppState.kskTime(_getFrameIndex(xPos));
			}
			
			private function _getFrameIndex(xPos:Number):int
			{
				var rawFrameIndex:Number = xPos/pixelPerFrame;
				return _slider.getNearestFrameValue(rawFrameIndex);
			}
			
			private function _framesToPixels(frameIndex:int, change:Number):Number
			{
				if(change < 0)
					return frameIndex*pixelPerFrame;			
				else
					return (frameIndex+1)*pixelPerFrame;
			}
			
			/**
			 * Sets the dragging of a clicked widget
			 */
			private function _marker_MouseDown(event:MouseEvent):void
			{
				if(_appState.gestureMode)
					return;
				
				if(event.target is Marker)
				{
					stage.addEventListener(MouseEvent.MOUSE_MOVE,_widget_MouseMove,true, 0, true);
					stage.addEventListener(MouseEvent.MOUSE_UP,_widget_MouseUp, true, 0, true);
					
					_targetMarker = event.target as Marker;
					_startX = event.stageX - _appState.overViewTrackBox.x;
					_changeX = 0;
				}
			}
			
			private function _widget_MouseMove(event:MouseEvent):void
			{
				if(!_targetMarker)
					return;
				
				//Compute mouse drag _changeX
				//_changeX adheres strictly to frame boundaries.
				var currentMouseX:Number = event.stageX - _appState.overViewTrackBox.x;
				_changeX = currentMouseX - _startX;
				_changeX = _framesToPixels(_changeX/pixelPerFrame, _changeX);
				var xPos:Number = _targetMarker.originalPosition+_changeX;
				
				//Set limits for the mouse drag so the target marker does not move out of the
				//Defined track region
				if(xPos < 0)
					xPos = 0;
				else if(width < xPos)
					xPos = width;
				
				xPos = _timeToPosition(_positionToTime(xPos));
				_targetMarker.moveWithStacking(xPos);
				_changeX = _targetMarker.x - _targetMarker.originalPosition;
				
				_framesToPixels(_changeX/pixelPerFrame,_changeX);
				_targetMarker.moveFutureMarkers(_changeX);
				
				_renderBars();
			}
			
			private function _widget_MouseUp(event:MouseEvent):void
			{
				stage.removeEventListener(MouseEvent.MOUSE_MOVE,_widget_MouseMove,true);
				stage.removeEventListener(MouseEvent.MOUSE_UP,_widget_MouseUp, true);
				
				if(!_targetMarker)
					return;
				
				if(Math.abs(_changeX) < pixelPerFrame)
					return;

				//Actual condition will be for each marker moved
				//Do the following
				var keysToRetime:Vector.<IKeyFrame> = new Vector.<IKeyFrame>();
				var times:Vector.<Number> = new Vector.<Number>();
				var stackedMarker:Marker;
				var freeMarker:Marker;
				var retimeTo:Number;
				var keyInfo:Object;
				
				var objectIDs:Vector.<int> = new Vector.<int>();
				var keyTypes:Vector.<int> = new Vector.<int>();
				var keyTimes:Vector.<Number> = new Vector.<Number>();
				
				for each(stackedMarker in _selectedMarkers)
				{
					retimeTo = _positionToTime(stackedMarker.x);
					for each(keyInfo in stackedMarker.keyList)
					{
						keysToRetime.push(keyInfo.key);
						times.push(retimeTo);
						objectIDs.push(keyInfo.objectID);
						keyTypes.push(keyInfo.type);
						keyTimes.push(keyInfo.key.endTime);
					}
				}
				
				for each(freeMarker in _unselectedMarkers)
				{
					retimeTo = _positionToTime(freeMarker.x);
					for each(keyInfo in freeMarker.keyList)
					{
						keysToRetime.push(keyInfo.key);
						times.push(retimeTo);
						objectIDs.push(keyInfo.objectID);
						keyTypes.push(keyInfo.type);
						keyTimes.push(keyInfo.key.endTime);
					}
				}
				
				KLogger.logRetimeKeys(objectIDs,keyTypes,keyTimes,times,_appState.time);
				
				var retimeOp:IModelOperation = _facade.retimeKeys(keysToRetime, times, _appState.time);
				_appState.addOperation(retimeOp);
			}
			
			/**
			 * rerenders all the bars in this time widget
			 */
			private function _renderBars():void
			{
				for each(var activityBar:ActivityBar in _activityBars)
				{
					activityBar.renderBar();
				}
			}
			
			/**
			 * Sort compare function
			 */
			private function _compareMarkerPosition(marker1:Marker, marker2:Marker):Number
			{
				if(marker1.x <= marker2.x)
					return -1;
				else
					return 1;				
			}
			
			private function _computeAppMaxTime(maxTime:Number):Number
			{
				var correctMaxTime:Number = 0;
				
				if(maxTime <= 0.8*KAppState.DEFAULT_MAX_TIME)
					correctMaxTime = KAppState.DEFAULT_MAX_TIME;
				else
					correctMaxTime = (maxTime/KAppState.DEFAULT_MAX_TIME)*KAppState.DEFAULT_MAX_TIME+KAppState.DEFAULT_MAX_TIME;
				
				return correctMaxTime;
			}

			
			/**
			 * Removes all markers from the container which holds the time markers
			 */
			public function clearTimeWidget():void
			{
				selectedBarContainer.removeAllElements();
				selectedMarkerContainer.removeAllElements();
				barContainer.removeAllElements();
				markerContainer.removeAllElements();
			}
			
			/**
			 * Generates activity bars for the the given type of markers
			 */
			private function _generateActivityBars(markers:Vector.<Marker>,selected:Boolean):void
			{
				var targetBarContainer:Group;
				if(selected)
					targetBarContainer = selectedBarContainer;
				else
					targetBarContainer = barContainer
				
				var newActivityBar:ActivityBar;

				var rightKeyInfoList:Vector.<Object>;
				var leftKeyInfoList:Vector.<Object>;
				var currentRightKeyInfo:Object;
				var currentRightKey:IKeyFrame;
				var currentLeftKeyInfo:Object;
				var currentLeftKey:IKeyFrame;

				var currentRightMarker:Marker;
				var currentLeftMarker:Marker;
				var targetLeftMarker:Marker;
				
				var i:int;
				var j:int;
				
				for(i = markers.length-1; -1 < i; i--)
				{
					targetLeftMarker = null;
					
					currentRightMarker = markers[i];
					rightKeyInfoList = currentRightMarker.keyList;
					
					//Find target left marker
					//Left marker must be the marker furthest away from the current right marker
					for each(currentRightKeyInfo in rightKeyInfoList)
					{
						currentRightKey = currentRightKeyInfo.key;
						j = i-1;
						
						if(!(currentRightKey is ISpatialKeyframe))
							continue;
						
						for(j; -1 < j; j--)
						{
							currentLeftMarker = markers[j];
							leftKeyInfoList = currentLeftMarker.keyList;
							
							for each(currentLeftKeyInfo in leftKeyInfoList)
							{
								currentLeftKey = currentLeftKeyInfo.key;
								
								if(currentLeftKey == currentRightKey.previous)
								{
									targetLeftMarker = currentLeftMarker;
									break;
								}
							}
						}
						
						if(targetLeftMarker)
						{
							newActivityBar = new ActivityBar();
							newActivityBar.initBar(targetLeftMarker, currentRightMarker);
							targetBarContainer.addElement(newActivityBar);
							_activityBars.push(newActivityBar);
						}
					}
				}
			}
			
		]]>
	</fx:Script>
	
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	<s:Rect height="100%" id="bgFill">
		<s:fill>
			<s:LinearGradient rotation="90">
				<s:GradientEntry color="0x36466E"/>
				<s:GradientEntry color="0x557695"/>
			</s:LinearGradient>
		</s:fill>
	</s:Rect>
	<s:Group width="100%" height="100%">
		<s:layout>
			<s:VerticalLayout verticalAlign="middle"/>
		</s:layout>
		<s:Group id="barContainer"/>
	</s:Group>
	<s:Group width="100%" height="100%">
		<s:layout>
			<s:VerticalLayout verticalAlign="middle"/>
		</s:layout>
		<s:Group id="markerContainer"/>
	</s:Group>
	<s:Group width="100%" height="100%">
		<s:layout>
			<s:VerticalLayout verticalAlign="middle"/>
		</s:layout>
		<s:Group id="selectedBarContainer"/>
	</s:Group>
	<s:Group width="100%" height="100%">
		<s:layout>
			<s:VerticalLayout verticalAlign="middle"/>
		</s:layout>
		<s:Group id="selectedMarkerContainer"/>
	</s:Group>
</s:BorderContainer>
