<?xml version="1.0" encoding="utf-8"?>
<!--
*Copyright 2010-2012 Singapore Management University
*Developed under a grant from the Singapore-MIT GAMBIT Game Lab

*This Source Code Form is subject to the terms of the
*Mozilla Public License, v. 2.0. If a copy of the MPL was
*not distributed with this file, You can obtain one at
*http://mozilla.org/MPL/2.0/.
-->
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009" 
					   xmlns:s="library://ns.adobe.com/flex/spark" 
					   xmlns:mx="library://ns.adobe.com/flex/mx"
					   showStatusBar="false"
					   minWidth="1280" minHeight="720"
					   width="1280" height="720"
					   creationComplete = "init()" xmlns:components1="sg.edu.smu.ksketch2.components.*" xmlns:ksketch2="sg.edu.smu.ksketch2.*"
					    activate="main.focusApp()" closing="_closeHandler(event)">
	<fx:Script>
		<![CDATA[
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			import mx.managers.PopUpManager;
			
			import sg.edu.smu.ksketch2.utils.ExportUtil;
			import sg.edu.smu.ksketch2.utils.ExportViewer;
			import sg.edu.smu.ksketch2.utils.KExportEvent;
			import sg.edu.smu.ksketch2.utils.KIOEvent;
			
			private var _fileRef:FileReference;
			
			public function init():void
			{
				this.maximize();
				main.init();
				main.addEventListener(KIOEvent.EVENT_SAVE, _saveHandler);
				main.addEventListener(KIOEvent.EVENT_LOAD, _loadHandler);
				main.addEventListener(KExportEvent.EVENT_EXPORT, _exportHandler);
			}
			
			private function _saveHandler(event:KIOEvent):void
			{
				var fileRef:FileReference = new FileReference();
				if(event.saveData)
				{
					var date:Date = new Date();
					fileRef.save(event.saveData, date.fullYear.toString()+"_"+(date.month+1).toString()+"_"+date.date.toString()
						+"_"+date.hours.toString()+date.minutes.toString()+".kmv");
					fileRef.addEventListener(Event.COMPLETE,
						function tempSaveHandler(saveCompleteEvent:Event):void
						{
							if(event.closing)
								exit();
						}
						);
					
					fileRef.addEventListener(Event.CANCEL,
						function tempSaveHandler(saveCompleteEvent:Event):void
						{
							if(event.closing)
								exit();
						}
					);
				}
			}
			
			private function _loadHandler(event:Event):void
			{
				_fileRef = new FileReference();
				_fileRef.addEventListener(Event.SELECT, _handleFileSelected);
				var fileFilter:FileFilter = new FileFilter("KSketch Files", "*.kmv", "*.kmv");
				_fileRef.browse([fileFilter]);
			}
			
			private function _handleFileSelected(event:Event):void
			{
				_fileRef = event.target as FileReference;
				_fileRef.removeEventListener(Event.COMPLETE, _handleFileSelected);
				_fileRef.addEventListener(Event.COMPLETE, _handleFileLoadComplete);
				_fileRef.load();
			}
			
			private function _handleFileLoadComplete(event:Event):void
			{
				var data:ByteArray = _fileRef.data;
				var readXML:XML = new XML(data);
				main.loadFile(readXML);
			}
			
			private function _exportHandler(event:KExportEvent):void
			{
				if(event.data)
				{
					var viewer:ExportViewer = new ExportViewer();
					viewer.open(this, true);
					
					PopUpManager.centerPopUp(viewer);
					viewer.addEventListener(Event.REMOVED_FROM_STAGE, 
						function handleExportComplete(event:Event):void
						{
							var fileRef:FileReference = new FileReference();
							var date:Date = new Date();
							fileRef.save(viewer.videoBuffer, date.fullYear.toString()+"_"+(date.month+1).toString()+"_"+date.date.toString()
								+"_"+date.hours.toString()+date.minutes.toString()+".flv");
						}
						,false
						,0
						,true);

						viewer.encode(event.data, ExportUtil.WIDTH_480P, ExportUtil.HEIGHT_480P);
				}
			}
			
			private function _closeHandler(event:Event):void
			{
				if(main.scenePresent())
				{
					event.preventDefault();
					Alert.show("Would you like to save before exiting KSketch?","Confirm Exit",Alert.YES|Alert.NO,null,_closeWindow,null,Alert.NO);
				}
			}
			
			private function _closeWindow(event:CloseEvent):void
			{
				if(event.detail == Alert.YES)
					main.triggerSave();
				else
					exit();
			}
		]]>
	</fx:Script>
	<ksketch2:KSketch2Interface id="main"/>
</s:WindowedApplication>
