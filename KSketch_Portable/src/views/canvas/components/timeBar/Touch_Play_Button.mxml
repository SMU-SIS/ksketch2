<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" width="50" height="50">
	<fx:Script>
		<![CDATA[
			import sg.edu.smu.ksketch2.controls.widgets.KTimeControl;
			import sg.edu.smu.ksketch2.utils.KSelection;
			
			import views.canvas.KSketch_CanvasView;
			import views.canvas.interactioncontrol.KMobileInteractionControl;
			
			private const _FILL:uint = 0x000000;
			private var _interceptor:DisplayObject;
			private var _interactionControl:KMobileInteractionControl;
			private var _timeControl:KTouchTimeControl;
			private var _buttonState:int;
			private var _oldSelection:KSelection;
			
			private const SHOW_PLAY:int = 0;
			private const SHOW_PAUSE:int = 1;
			
			public function init(timeControl:KTouchTimeControl, interactionControl:KMobileInteractionControl, interceptor:DisplayObject):void
			{
				_interactionControl = interactionControl;
				_timeControl = timeControl;
				_interceptor = interceptor;
				buttonState = SHOW_PLAY;
				
				timeControl.addEventListener(KTimeControl.PLAY_START, _handlePlayPause);
				timeControl.addEventListener(KTimeControl.PLAY_STOP, _handlePlayPause);
			}
			
			public function playToggle():void
			{
				if(_buttonState == SHOW_PLAY)
					_timeControl.play();
				else
					_timeControl.stop();
			}
			
			private function _handlePlayPause(event:Event):void
			{
				if(event.type == KTimeControl.PLAY_START)
				{
					buttonState = SHOW_PAUSE;
					_oldSelection =_interactionControl.selection;
					_interactionControl.selection = null;
					_interceptor.visible = true;
				}
				else
				{
					buttonState = SHOW_PLAY;	// IF IT IS PAUSING,THEN SHOW PLAY!
					_interactionControl.selection = _oldSelection;
					_oldSelection = null;
					_interceptor.visible = false;
				}
			}
			
			private function set buttonState(value:int):void
			{
				graphics.clear();
				_buttonState = value;
				if(_buttonState == SHOW_PLAY)
				{
					graphics.beginFill(KSketch_CanvasView.CANVAS_BUTTON_COLOR, KSketch_CanvasView.CANVAS_BUTTON_BLEND);
					graphics.moveTo(37.5, 25);
					graphics.lineTo(12.5, 12.5);
					graphics.lineTo(12.5, 37.5);
					graphics.lineTo(37.5, 25);
					graphics.endFill();
				}
				else
				{
					graphics.beginFill(KSketch_CanvasView.CANVAS_BUTTON_COLOR, KSketch_CanvasView.CANVAS_BUTTON_BLEND);
					graphics.drawRect(15,12.5,7.5,25);
					graphics.endFill();
					
					graphics.beginFill(KSketch_CanvasView.CANVAS_BUTTON_COLOR, KSketch_CanvasView.CANVAS_BUTTON_BLEND);
					graphics.drawRect(27.5,12.5,7.5,25);
					graphics.endFill();
					

				}
				
				graphics.lineStyle(1, KSketch_CanvasView.CANVAS_BUTTON_COLOR, KSketch_CanvasView.CANVAS_DIVIDER_BLEND);
				graphics.moveTo(47,5);
				graphics.lineTo(47,45);
			}
		]]>
	</fx:Script>
	<s:Rect top="1" bottom="1" right="1" left="1">
		<s:fill>
			<s:SolidColor color="0xFF0000"/>
		</s:fill>
	</s:Rect>
</s:Group>
