<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" width="40" height="40">
	<fx:Script>
		<![CDATA[
			import sg.edu.smu.ksketch2.controls.widgets.KTimeControl;
			import sg.edu.smu.ksketch2.utils.KSelection;
			
			import views.canvas.interactioncontrol.KMobileInteractionControl;
			
			private var _interactionControl:KMobileInteractionControl;
			private var _timeControl:KTouchTimeControl;
			private var _buttonState:int;
			private var _oldSelection:KSelection;
			
			private const SHOW_PLAY:int = 0;
			private const SHOW_PAUSE:int = 1;
			
			public function init(timeControl:KTouchTimeControl, interactionControl:KMobileInteractionControl):void
			{
				_interactionControl = interactionControl;
				_timeControl = timeControl;
				buttonState = SHOW_PLAY;
				
				timeControl.addEventListener(KTimeControl.PLAY_START, _handlePlayPause);
				timeControl.addEventListener(KTimeControl.PLAY_STOP, _handlePlayPause);
			}
			
			public function playToggle():void
			{
				if(_buttonState == SHOW_PLAY)
					_timeControl.play();
				else
					_timeControl.stop();
			}
			
			private function _handlePlayPause(event:Event):void
			{
				if(event.type == KTimeControl.PLAY_START)
				{
					buttonState = SHOW_PAUSE;
					_oldSelection =_interactionControl.selection;
					_interactionControl.selection = null;
				}
				else
				{
					buttonState = SHOW_PLAY;	// IF IT IS PAUSING,THEN SHOW PLAY!
					_interactionControl.selection = _oldSelection;
					_oldSelection = null;
				}
			}
			
			private function set buttonState(value:int):void
			{
				graphics.clear();
				_buttonState = value;
				if(_buttonState == SHOW_PLAY)
				{
					graphics.beginFill(0x000000);
					graphics.lineStyle(1);
					graphics.moveTo(5,5);
					graphics.lineTo(5,35);
					graphics.lineTo(35,20);
					graphics.lineTo(5,5);
					graphics.endFill();
				}
				else
				{
					graphics.lineStyle(7.5,0x000000);
					graphics.moveTo(10,10);
					graphics.lineTo(10,30);
					graphics.moveTo(25,10);
					graphics.lineTo(25,30);
				}
			}
		]]>
	</fx:Script>
	<s:Rect width="100%" height="100%" alpha="1">
		<s:fill>
			<s:SolidColor color="0x00FFFF"/>
		</s:fill>
	</s:Rect>
</s:Group>
